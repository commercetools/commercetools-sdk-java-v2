import com.commercetools.sdk.plugins.GraphQLPlugin

plugins {
    id "com.netflix.dgs.codegen" version "8.3.0"
    id 'java'
}

apply plugin:'com.netflix.dgs.codegen'
apply plugin: GraphQLPlugin

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.register('integrationTestJar', Jar) {
    archiveClassifier = 'test-utils'
    from sourceSets.integrationTest.output
}

configurations {
    integrationTestArtifacts
}

artifacts {
    integrationTestArtifacts integrationTestJar
}

dependencies {
    api project(':commercetools:commercetools-sdk-java-api')
    testImplementation project(':commercetools:commercetools-http-client')
    implementation google.findbugs
    implementation javax.validation
    api graphql.java
    api jackson_core.databind
    api jackson_core.core
    api jackson_core.datatype
    api 'com.netflix.graphql.dgs.codegen:graphql-dgs-codegen-shared-core:8.3.0'

}

generateGraphQLRoots {
    dependsOn "generateJava"
}

generateJava {
    schemaPaths = ["${projectDir}/src/main/resources/graphql"] // List of directories containing schema files
    packageName = 'com.commercetools.graphql.api' // The package name to use to generate sources
    generateBoxedTypes = true
    generateClientv2 = true
    shortProjectionNames = true

    typeMapping = [
            "Country": "String",
            "Locale": "String",
            "Long": "Long",
            "Json": "com.fasterxml.jackson.databind.JsonNode",
            "KeyReferenceInput": "String",
            "Set": "Object",
            "Time": "String",
            "BigDecimal": "Double",
            "YearMonth": "String",
            "SearchValueType": "Object"
    ]
}
