plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.4'
    id 'io.spring.dependency-management' version '1.1.0'
    id "de.undercouch.download" version "5.3.0"
}

group = 'com.commercetools.sdk.examples'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    versions = [
            commercetools: "14.2.0-SNAPSHOT",
            newrelic: "8.0.1"
    ]
}

dependencies {
    implementation "com.commercetools.sdk:commercetools-sdk-java-api:${versions.commercetools}"
    implementation "com.commercetools.sdk:commercetools-apachehttp-client:${versions.commercetools}"
    implementation "com.commercetools.sdk:commercetools-monitoring-newrelic:${versions.commercetools}"
    implementation "com.newrelic.agent.java:newrelic-api:${versions.newrelic}"

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    developmentOnly "org.springframework.boot:spring-boot-devtools"
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('downloadNewrelic', Download) {
    mkdir 'newrelic'
    src 'https://download.newrelic.com/newrelic/java-agent/newrelic-agent/current/newrelic-java.zip'
    dest file('newrelic')
}
tasks.register('unzipNewrelic', Copy) {
    dependsOn downloadNewrelic
    from zipTree(file('newrelic/newrelic-java.zip'))
    into rootDir
}

if (project.file("newrelic/newrelic.jar").exists()) {
    tasks.withType(JavaExec)
            {
                jvmArgs "-javaagent:newrelic/newrelic.jar"
            }
}
